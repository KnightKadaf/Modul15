#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// 10.03.24 Переводим ПриДобавленииКомандПечати = Истина; 
// Параметры:
// НастройкиОбъекта - Неопределено - передаваемый объект	 
Процедура ПриОпределенииНастроекПечати(НастройкиОбъекта) Экспорт

	НастройкиОбъекта.ПриДобавленииКомандПечати = Истина;
	
КонецПроцедуры  


// 10.03.24 Добавляем комапды печати 
// Параметры:
// КомандыПечати - Неопределено - передаваемый объект
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт	
	
	// Анкета
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Анкета";
	КомандаПечати.Представление = НСтр("ru = 'Анкета'");
	КомандаПечати.Порядок = 5;
	
	// Транспортная накладная
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ТранспортнаяНакладная";
	КомандаПечати.Представление = НСтр("ru = 'Транспортная накладная'");
	КомандаПечати.Порядок = 10;

	// Договор на доставку
	КомандаПечати = КомандыПечати.Добавить();
    КомандаПечати.МенеджерПечати = "УправлениеПечатью";
	КомандаПечати.Идентификатор = "Документ.МУ_Доставка.ПФ_DOC_ДоговорНаДоставку";
	КомандаПечати.Представление = НСтр("ru = 'Договор на доставку'");
	КомандаПечати.Порядок = 20;
	
	// Комплект документов
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Анкета,ТранспортнаяНакладная";
	КомандаПечати.Представление = НСтр("ru = 'Комплект документов'");
	КомандаПечати.Порядок = 75;
КонецПроцедуры

// 10.03.24 Выбор печатной формы
// Параметры:
// МассивОбъектов - Неопределено - передаваемый объект
// ПараметрыПечати - Неопределено - передаваемый объект
// КоллекцияПечатныхФорм - Неопределено - передаваемый объект
// ОбъектыПечати - Неопределено - передаваемый объект
// ПараметрыВывода - Неопределено - передаваемый объект
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "Анкета");
	Если ПечатнаяФорма <> Неопределено Тогда
	    ПечатнаяФорма.ТабличныйДокумент = ПечатьАнкета(МассивОбъектов, ОбъектыПечати);
	    ПечатнаяФорма.СинонимМакета = НСтр("ru = 'Анкета'");
	    ПечатнаяФорма.ПолныйПутьКМакету = "Документ.МУ_Доставка.ПФ_MXL_Анкета";
	КонецЕсли;
	
	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "ТранспортнаяНакладная");
	Если ПечатнаяФорма <> Неопределено Тогда
	    ПечатнаяФорма.ТабличныйДокумент = ПечатьТранспортнойНакладной(МассивОбъектов, ОбъектыПечати);
	    ПечатнаяФорма.СинонимМакета = НСтр("ru = 'Транспортная накладная'");
	    ПечатнаяФорма.ПолныйПутьКМакету = "Документ.МУ_Доставка.ПФ_MXL_ТранспортнаяНакладная";
	КонецЕсли;

	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции    

Функция ПечатьТранспортнойНакладной(МассивОбъектов, ОбъектыПечати)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.МУ_Доставка.ПФ_MXL_ТранспортнаяНакладная");
	
	ДанныеДокументов = ПолучитьДанныеДокументов(МассивОбъектов);
	
	ПервыйДокумент = Истина;
	
	Пока ДанныеДокументов.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ВывестиШапку(ДанныеДокументов, ТабличныйДокумент, Макет);
		ВывестиЗаголовок(ДанныеДокументов, ТабличныйДокумент, Макет);
		ВывестиТовары(ДанныеДокументов, ТабличныйДокумент, Макет);
		ВывестиПодпись(ДанныеДокументов, ТабличныйДокумент, Макет);
		

		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
            НомерСтрокиНачало, ОбъектыПечати, ДанныеДокументов.Ссылка);		
		
	КонецЦикла;	
		
	Возврат ТабличныйДокумент;	
КонецФункции


Функция ПечатьАнкета(МассивОбъектов, ОбъектыПечати)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.МУ_Доставка.ПФ_MXL_Анкета");
	
	ДанныеДокументов = ПолучитьДанныеДокументов(МассивОбъектов);
	
	ПервыйДокумент = Истина;
	
	Пока ДанныеДокументов.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ВывестиШапку(ДанныеДокументов, ТабличныйДокумент, Макет);

		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
            НомерСтрокиНачало, ОбъектыПечати, ДанныеДокументов.Ссылка);		
		
	КонецЦикла;	
		
	Возврат ТабличныйДокумент;	
КонецФункции


Функция ПолучитьДанныеДокументов(МассивОбъектов)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	МУ_Доставка.Ссылка КАК Ссылка,
	               |	МУ_Доставка.Номер КАК Номер,
	               |	МУ_Доставка.Дата КАК Дата,
	               |	МУ_Доставка.Организация КАК Организация,
	               |	МУ_Доставка.Контрагент КАК Контрагент,
	               |	МУ_Доставка.Товары.(
	               |		Ссылка КАК Товар,
	               |		НомерСтроки КАК НомерСтроки,
	               |		Номенклатура КАК Номенклатура,
	               |		Количество КАК Количество
	               |	) КАК Товары
	               |ИЗ
	               |	Документ.МУ_Доставка КАК МУ_Доставка
	               |ГДЕ
	               |	МУ_Доставка.Ссылка В (&Ссылка)";
	
	Запрос.Параметры.Вставить("Ссылка", МассивОбъектов);
	
	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции


Процедура ВывестиТовары(ДанныеДокументов, ТабличныйДокумент, Макет)
	
	ОбластьСтрока = Макет.ПолучитьОбласть("Таблица");
	
	ВыборкаТовары = ДанныеДокументов.Товары.Выбрать();
	Пока ВыборкаТовары.Следующий() Цикл
		ОбластьСтрока.Параметры.Заполнить(ВыборкаТовары);
		ТабличныйДокумент.Вывести(ОбластьСтрока);
	КонецЦикла;
	
КонецПроцедуры

Процедура ВывестиПодпись(ДанныеДокументов, ТабличныйДокумент, Макет)
	
	ОбластьПодпись = Макет.ПолучитьОбласть("Подпись");
	ТабличныйДокумент.Вывести(ОбластьПодпись);
	
КонецПроцедуры

Процедура ВывестиЗаголовок(ДанныеДокументов, ТабличныйДокумент, Макет)
	
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьЗаголовок.Параметры.Организация =  ДанныеДокументов.Организация;
	ОбластьЗаголовок.Параметры.Контрагент =  ДанныеДокументов.Контрагент;
	ТабличныйДокумент.Вывести(ОбластьЗаголовок);
	
КонецПроцедуры


Процедура ВывестиШапку(ДанныеДокументов, ТабличныйДокумент, Макет)
	
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьШапка.Параметры.Номер = ДанныеДокументов.Номер;  
	ОбластьШапка.Параметры.Дата = Формат(ДанныеДокументов.Дата,"ДЛФ=DD"); 
	ДанныеQRКода = ГенерацияШтрихкода.ДанныеQRКода(ПолучитьНавигационнуюСсылку(ДанныеДокументов.Ссылка), 1, 120);
	Если НЕ ТипЗнч(ДанныеQRКода) = Тип("ДвоичныеДанные") Тогда
		ТекстСообщения = НСтр("ru = 'Не удалось сформировать QR-код.
		|Технические подробности см. в журнале регистрации.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
	Иначе
		КартинкаQRКода = Новый Картинка(ДанныеQRКода);
		ОбластьШапка.Рисунки.КуЭрКод.Картинка = КартинкаQRКода;
	КонецЕсли;
	ТабличныйДокумент.Вывести(ОбластьШапка);

КонецПроцедуры
#КонецОбласти    

#КонецЕсли